C─────────────────────────────────────────────────────────────────────────────┐
C        Name: RBYY                                       01/12/92 02:15pm    │
C                                                                             │
C Description: Перебор ансамблей переменных и числа аналогов,                 │
C              расчет критерия скользящего определения                        │
C                                                                             │
C Copyright (C) Nataly and Gregory Ivakhnenko, 1990-1991. All Rights Reserved │                               }
C─────────────────────────────────────────────────────────────────────────────┘

      subroutine RBYY (CVII,JMC,JFC,MY,MV,N,JJFR,MMY,MM,NN,NN1,
     +Y,XV,X,XA,XB,CL,RAS,CVJ,V,IVM,IL,IMX,IN,LA)

      real Y(MMY,NN),   XV(MM,NN1), X(MM,NN1), XA(MM,NN), XB(MM,NN),
     +     CL(JJFR,NN), RAS(NN,NN), CVJ(MM),   V(NN)

      integer IVM(JJFR,NN), IL(MM), IMX(MM), IN(NN)

      logical*1 LA(MM)
      common /LMAIN/ JP,JFM
      automatic I,J,K

      EPS=1.E10
      CVII=EPS
      call RAST (MY,N,MMY,NN,Y,RAS)
      call FRMIN (JFM,N,JJFR,NN,CL,RAS,V,IVM,IN)
C --------------------------------------------------------------------------
C                   Перебор  количества аналогов
C --------------------------------------------------------------------------
       do 520 JF=1,JFM
         do 10 J=1,MV
            IL(J)=J
10          LA(J)=.false.
C --------------------------------------------------------------------------
C                   Перебор ансамблей аргументов
C --------------------------------------------------------------------------
         do 540 JM=MV,1,-1
C --------------------------------------------------------------------------
C                Блок формирования массивов прогнозов X, (XA)
C --------------------------------------------------------------------------
           K=0
           do 190 J=1,MV
              if(.not.LA(J))  then
                 K=K+1
                 do 180 I=1,N
180              X(K,I)=XV(J,I)
              endif
190        continue

           if(JF.eq.1)  then
              do 200 I=1,N
                 K=IVM(1,I)
                    do 200 J=1,JM
200                 XA(J,I)=X(J,K)
              goto 340
           endif
C --------------------------------------------------------------------------
C               Расчет весовых коэффициентов
C --------------------------------------------------------------------------
      do 220 J=1,JM
         do 220 I=1,N
220      XA(J,I)=0.0

      do 260 I=1,N
         do 260 JA=1,JF
         K=IVM(JA,I)
            do 260 J=1,JM
260         XA(J,I)=XA(J,I)+X(J,K)*CL(JA,I)

      do 320 I=1,N
         S=0.
            do 300 JA=1,JF
300         S=S+CL(JA,I)
         do 320 J=1,JM
            if(S.eq.0.) then
               XA(J,I)=EPS
               goto 320
            endif
            XA(J,I)=XA(J,I)/S
320      continue

340   do 360 J=1,JM
         do 360 I=1,N
360      XB(J,I)=X(J,I)
C --------------------------------------------------------------------------
C                     Расчет массива ошибок прогнозов
C --------------------------------------------------------------------------
      do 400 J=1,JM
         do 400 I=1,N
400      XA(J,I)=abs(XB(J,I)-XA(J,I))
C --------------------------------------------------------------------------
C                     Расчет критерия CVJ по столбцам XA
C --------------------------------------------------------------------------
      do 450 J=1,JM
        S=0.
            do 470 I=1,N
470         S=S+XA(J,I)
450      CVJ(J)=S/N
C --------------------------------------------------------------------------
C          Расчет критерия скользящего контроля по всей матрице XA(JM x N)
C --------------------------------------------------------------------------
      S=0.
      do 440 I=1,N
        AMIN=EPS
           do 460 J=1,JM
460        if(XA(J,I).lt.AMIN) AMIN=XA(J,I)
440     S=S+AMIN
C --------------------------------------------------------------------------
C           Запись минимального критерия и соответствующего ансамбля
C --------------------------------------------------------------------------
      CV=S/N
      if(CV.le.CVII) then
         I=0
         do 500 J=1,MV
            if(.not.LA(J)) then
                I=I+1
                IMX(I)=J
500         endif
         CVII=CV
         JMC=JM
         JFC=JF
      endif
C --------------------------------------------------------------------------
C               Селекция ансамблей аргументов по минимуму критерия CV
C --------------------------------------------------------------------------
      call RA1IV (JM,MM,CVJ,IL)
      LA(IL(JM))=.true.

540      continue
520   continue
      return
      end
